import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        maven{ url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
    }

    plugins {
        id 'com.android.application' version '7.1.0-alpha13'
        id 'com.android.library' version '7.1.0-alpha13'
        id 'org.jetbrains.kotlin.android' version '1.5.21'
    }
    resolutionStrategy {
        eachPlugin {
            if (it.requested.id.namespace == "com.android") {
                it.useModule("com.android.tools.build:gradle:7.0.0")
            }
            if (it.requested.id.id == "kotlin-multiplatform") {
                it.useModule("org.jetbrains.kotlin:kotlin-gradle-plugin:${requested.version}")
            }
        }
    }
}

rootProject.name = 'constraintlayout'

include ':core',
//        ':constraintlayout',
        ':tools',
        ':compose',
        ':swing',
        ':composedemo',
        ':desktop'

includeBuild 'build-logic'

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        google()
        maven { url "../../prebuilts/tools/common/m2/repository" }
        maven { url "https://androidx.dev/snapshots/builds/7352378/artifacts/repository/" }
        maven{ url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
    }
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
}
